// all-sensor

plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services' // Firebase
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    compileSdk  34
    namespace "com.landenlabs.all_sensor"

    defaultConfig {
        applicationId "com.landenlabs.all_sensor"
        minSdkVersion 23
        targetSdkVersion 34

        versionCode 40801
        versionName '4.08.01'

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        buildConfigField "long", "BuildTimeMilli", System.currentTimeMillis() + "L"
        // TODO - load use/pwd from local.properties
        // buildConfigField "String", "GoveeUser1", "\"${GoveeUser1}\""
        // buildConfigField "String", "GoveePwd1", "\"${GoveePwd1}\""

        buildConfigField "String", "GoveeUser1", "\"test\""
        buildConfigField "String", "GoveePwd1", "\"test\""
    }

    // Save app version in string so resource can be used in layouts
    // BuildConfig.VERSION_NAME already has version
    applicationVariants.configureEach { variant ->
        variant.resValue "string", "versionName", "v" + variant.versionName
    }


    buildFeatures.buildConfig = true

    buildTypes {
        release {
            // Enables code shrinking, obfuscation, and optimization.
            minifyEnabled true

            // List of resources removed at end of generated mapping file:
            // grep unused ./app/build/outputs/mapping/demoRelease/resources.txt
            shrinkResources true

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    testOptions {
        unitTests {
            // http://robolectric.org/getting-started/
            includeAndroidResources = true
        }
    }

    // Doc- https://developer.android.com/reference/tools/gradle-api/4.1/com/android/build/api/dsl/LintOptions
    lint {
        quiet false                  // turn off analysis progress reporting
        abortOnError true           // stop the gradle build if errors are found
        checkReleaseBuilds true
        // ignoreWarnings true     // if true, only report errors
        showAll true
        explainIssues true
        noLines false
        ignoreTestSources true
        checkOnly 'NewApi', 'MissingPermission', 'Deprecated'
        fatal 'NewApi'
        htmlReport true

        disable 'DefaultLocale', 'SimpleDateFormat', 'InflateParams', 'SpUsage',
                'UseCompoundDrawables', 'HandlerLeak', 'ViewConstructor', 'SetTextI18n',
                'RtlSymmetry', 'RtlHardcoded', 'ContentDescription',
                'MissingTranslation', 'ValidFragment', 'UnusedAttribute',
                 'LocalSuppress', 'HardcodedText', 'Typos',
                'TypographyFractions','TypographyQuotes'

        // 'FieldCanBeLocal', 'SpellCheckingInspection'
    }

    /*  Causes build crash - circular dependency
    applicationVariants.all {
        def lintTask = tasks["lint${name.capitalize()}"]
        assembleProvider.get().dependsOn.add(lintTask)
    }
     */
}

repositories {
    // https://github.com/PhilJay/MPAndroidChart
    maven { url 'https://jitpack.io' }
}

dependencies {

    // API 33
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-savedstate:2.8.2'
    implementation 'androidx.work:work-runtime:2.9.0'
    implementation 'androidx.sqlite:sqlite:2.4.0'
    implementation 'androidx.navigation:navigation-fragment:2.5.3'
    implementation 'androidx.navigation:navigation-ui:2.5.3'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'

    implementation "net.sourceforge.streamsupport:android-retrostreams:1.7.4"
    implementation "net.sourceforge.streamsupport:android-retrofuture:1.7.4"

    implementation 'androidx.annotation:annotation:1.8.0'
    // implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'

    // okHTTP
    // implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'
    // implementation 'com.squareup.okhttp3:okhttp:4.8.0'
    // https://square.github.io/okhttp/changelog/
    // https://github.com/square/okhttp/releases
    // https://pangea.weather.com/android/docs/installation
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'

    // GPS - Location
    implementation 'com.google.android.gms:play-services-location:21.3.0'

    // Firebase
    // Import the Firebase BoM
    implementation platform('com.google.firebase:firebase-bom:33.1.1')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-firestore'
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.firebaseui:firebase-ui-auth:8.0.2'
    // implementation 'com.google.firebase:firebase-database'
    // implementation 'com.firebaseui:firebase-ui-database:8.0.0'
    // Needed to fix a dependency conflict with FirebaseUI'
    // implementation 'androidx.arch.core:core-runtime:2.1.0'

    /*
    // Google Sheets
    implementation 'com.google.android.gms:play-services-auth:20.1.0'
    implementation('com.google.api-client:google-api-client-android:2.2.0') {
        exclude group: 'com.google.guava'
    }
    // implementation('com.google.apis:google-api-services-sheets:v4-rev581-1.25.0') {
    implementation('com.google.apis:google-api-services-sheets:v4-rev516-1.23.0') {
            exclude group: 'com.google.guava'
    }
     */

    // Graphing libraries
    implementation('com.androidplot:androidplot-core:1.5.10') {
        // http://halfhp.github.io/androidplot/docs/release_notes.html
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'   // https://github.com/PhilJay/MPAndroidChart/releases

    // Support libs
    implementation 'com.squareup.retrofit2:converter-gson:2.11.0'
    implementation 'net.danlew:android.joda:2.12.7'
    // https://github.com/dlew/joda-time-android/blob/main/CHANGELOG.md

    // Testing
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation 'androidx.arch.core:core-testing:2.2.0'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.6.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    testImplementation 'androidx.arch.core:core-testing:2.2.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.assertj:assertj-core:3.26.0'
    testImplementation 'org.robolectric:robolectric:4.12.2'
    testImplementation 'org.mockito:mockito-core:5.12.0'
    testImplementation 'androidx.test:core:1.6.1'
    testImplementation 'joda-time:joda-time:2.12.7'
    // https://github.com/JodaOrg/joda-time/releases

    // testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
}

// Rename app
android.applicationVariants.configureEach { variant ->
    variant.outputs.all {
        def ourname = project.parent.name
        outputFileName = "$ourname-${variant.name}-${variant.versionName}.apk"
        println "out name= $outputFileName"
    }
}

// ./gradlew extractStringsFromLayouts
// apply plugin: 'android-string-extractor'

//
// ./gradlew :sensor:generateDependencyGraph
// open  sensor/build/reports/dependency-graph/dependency-graph.png
// apply plugin: "com.vanniktech.dependency.graph.generator"

